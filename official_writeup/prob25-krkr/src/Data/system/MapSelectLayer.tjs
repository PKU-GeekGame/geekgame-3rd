// MapMapSelectLayer.tjs - マップxk肢レイヤ
// Copyright (C)2001-2006, W.Dee and contributors  改?配布は自由です


class MapSelectButtonLayer extends Layer
{
    var pos; // 鏊指定のh用

    var onenter; // マウスが入ってきたときにg行するもの
    var onleave; // マウスが出ていったときにg行するもの
    var onclick; // クリックrにg行するもの
    var storage; // I理K了後のジャンプ先
    var target;  // I理K了後のジャンプ先
    
    var Butt_mouseOn = false; // レイヤ内にマウスがあるか
    var Butt_keyPressed = false;

    var baseCount; // アニメ`ションパタ`ンの数
    
    /**
     * コンストラクタ
     * @param win ウインドウ
     * @param par Hレイヤ
     * @param elm 初期化用パラメ`タ
     */
    function MapSelectButtonLayer(win, par, elm) {
        
        super.Layer(win, par);

        // ウインドウからカ`ソルをコピ`
        if (typeof win.cursorPointed !== "undefined") {
            cursor = win.cursorPointed;
        }
        
        hitType = htMask;
        hitThreshold = 1;
        focusable = true; // フォ`カスを得られる

        // 画像のロ`ド
        loadImages(elm.image);
        setSize(elm.width === void ? imageWidth / 2 : elm.width, elm.count !== void ? imageHeight / elm.count : elm.height);
        //dm("按钮生成:" + elm.image + " size:" + width + "," + height);
        
        // アニメ`ションパタ`ン数（k方向）
        baseCount = (int)(imageHeight / height);

        update();
    }

    function finalize() {
        super.finalize();
    }
    
    /**
     * レイヤ描画I理
     */
	function onPaint() {
        super.onPaint(...);

        if (Butt_mouseOn) {
            imageLeft = -width;
            if (baseCount > 1) {
                var t = int(System.getTickCount() / parent.baseInterval);
                t %= baseCount;
                imageTop = -height * t;
            } else {
                imageTop = 0;
            }
        } else {
            imageLeft = 0;
            imageTop  = 0;
        }
    }
    
	function onMouseUp(x, y, button, shift)	{
        if (enabled && button == mbLeft) {
            parent.onButtonClick(this);
        }
        if (this isvalid) super.onMouseUp(...);
    }

	function onMouseEnter()	{
        // マウスがレイヤI域内に入った
        if(onenter !== void) Scripts.eval(onenter);
		update();
		Butt_mouseOn = true;
		super.onMouseEnter(...);
        focus();
	}

	function onMouseLeave()	{
        // マウスがレイヤI域から出ていった
        if(onleave !== void) Scripts.eval(onleave);
		update();
		Butt_mouseOn = false;
		super.onMouseLeave(...);
	}

	function onKeyDown(key, shift, process)
	{
        // キ`が押された
        if(window.preProcessKeys(key, shift)) return;
        if (enabled && (key == VK_SPACE || key == VK_RETURN)) {
            parent.onButtonClick(this);
        }
        if (this isvalid) super.onKeyDown(...);
    }

	function onFocus(prevfocused, direction)
	{
        // キ`操作によるフォ`カス婴龊悉衰蕙Ε攻`ソルも移
        var sgks = window.getKeyState;
        var process = sgks(VK_LEFT) || sgks(VK_UP) || sgks(VK_RIGHT) ||
            sgks(VK_DOWN) || sgks(VK_TAB);
        if(process) {
            cursorX = (width>>1);
            cursorY = (height>>1);
        }
    }
}

/**
 * xk肢I理用ロジック
 * KAG用に特化しています
 */
class MapSelectLayer extends KAGLayer {

    var msgoff = true;

    // ベ`スの画像
    var frameGraphic = ""; // フレ`ム画像ファイル名
    var frameKey = clNone; // フレ`ム画像キ`
    var frameColor = 0x000000; // フレ`ムの色
    var frameOpacity = 128; // フレ`ムの不透明度
    
    // マップ用ボタンのアニメパタ`ンのインタ`バル
    var baseInterval = 100;
    var buttonWidth  = 100;

    // 抗音
    var enterse;
    var leavese;
    var clickse;

    // タイマ
    var timer;
    
    /**
     * レイヤの内容消去
     */
	function clearLayer()
	{
        if (frameGraphic == "") {
            // フレ`ム画像が指定されていない龊
            face = dfAuto;
            if(type == ltAddAlpha) {
                fillRect(0, 0, imageWidth, imageHeight, (frameOpacity << 24) +
                         ((int)((((frameColor&0xff0000)>>16) * frameOpacity) / 255)<<16 ) +
                         ((int)((((frameColor&0x00ff00)>> 8) * frameOpacity) / 255)<< 8 ) +
                         ((int)((((frameColor&0x0000ff)    ) * frameOpacity) / 255)     ) );
            } else {
                fillRect(0, 0, imageWidth, imageHeight, (frameOpacity << 24) + frameColor);
            }
            //xoff = 0;
            //yoff = 0;
            
        } else {
            // センタリング表示
            var layer = new global.Layer(window, this);
            layer.loadImages(frameGraphic, frameKey);
            layer.setSizeToImageSize();
            
            // 消去
            fillRect(0, 0, imageWidth, imageHeight, 0);
            
            // 画像コピ`
            var xoff = ((width  - layer.width)>>1)  + left;
            var yoff = ((height - layer.height)>>1) + top;
            operateRect(xoff, yoff, layer, 0, 0, layer.width, layer.height);
            
            invalidate layer;
        }
        face = dfProvince;
        colorRect(0, 0, imageWidth, imageHeight, 0); // I域もクリア
        face = dfAuto;
    }

    /**
     * オプションのO定
     */
	function setOptions(elm) {

        super.setOptions(elm);

        // アニメ`ションのインタ`バル
        baseInterval = elm.interval if elm.interval !== void;
        buttonWidth  = elm.buttonwidth if elm.btnwidth !== void;
        
        // 抗音指定
        enterse = elm.enterse if elm.enterse !== void;
        leavese = elm.leavese if elm.leavese !== void;
        clickse = elm.clickse if elm.clickse !== void;

        // 背景グラフィック指定
        frameGraphic  = elm.frame    if elm.frame !== void;
        frameKey      = elm.framekey if elm.framekey !== void;
        frameColor    = +elm.color   if elm.color !== void;
        frameOpacity  = +elm.opacity if elm.opacity !== void;
        clearLayer();

        // メッセ`ジを消去するかどうか
        msgoff = elm.msgoff if elm.msgoff !== void;
    }

    // 登hgみボタン情
    var buttons = %[];

    // 登hgみ鏊情
    var positions = %[];
    
    /**
     * コンストラクタ
     */
    function MapSelectLayer(window) {
        super.Layer(window, window.primaryLayer);
        setImageSize(parent.width, parent.height);
        setSizeToImageSize();
        hitType      = htMask;
        hitThreshold = 1;
        focusable    = false;
        cursor = window.cursorDefault;
        
        // タイマ`を作成
        timer = new Timer(onTimer, '');
    }

    function finalize() {
        clear();
        invalidate buttons;
        invalidate positions;
        invalidate selects;
        invalidate timer;
        super.finalize(...);
    }

    function onTimer() {
        // タイマ`の周期ごとに呼ばれる
        for (var i=selects.count-1;i>=0;i--) {
            selects[i].update();
        }
	}

    
    /**
     * ボタン情螭巫芳
     */
    function addButton(elm) {
        if (elm.name !== void) {
            var e = %[];
            e.image  = elm.image;
            e.width  = +elm.width  if elm.width  !== void;
            e.height = +elm.height if elm.height !== void;
            e.count  = +elm.count  if elm.count  !== void;
            buttons[elm.name] = e;
        }
    }

    /**
     * 鏊情螭巫芳
     */
    function addPosition(elm) {
        if (elm.name !== void) {
            var e = %[];
            e.left = +elm.left;
            e.top  = +elm.top;
            positions[elm.name]  = e;
        }
    }

    /**
     * ボタン情螭鏊情螭蜗去
     */
    function init() {
        (Dictionary.clear incontextof buttons)(); 
        (Dictionary.clear incontextof positions)(); 
    }
    
    // 登hgみxk肢
    var selects = [];
    
    /**
     * xk消去
     */
    function clear() {
        for (var i=selects.count-1;i>=0;i--) {
            var select = selects[i];
            invalidate select;
        }
        selects.clear();
    }

    /**
     * xk肢を追加
     */
    function addSelect(elm) {

        var info = buttons[elm.text];
        if (info === void) {
            throw new Exception("无法追加制定的选项:" + elm.text);
        }
        
        var enterse  = elm.enterse !== void ? elm.enterse : enterse;
        var leavese  = elm.leavese !== void ? elm.leavese : leavese;
        var clickse  = elm.clickse !== void ? elm.clickse : clickse;

        var entersebuf = elm.entersebuf !== void ? elm.entersebuf : kag.numSEBuffers - 2;
        var leavesebuf = elm.leavesebuf !== void ? elm.leavesebuf : kag.numSEBuffers - 2;
        var clicksebuf = elm.clicksebuf !== void ? elm.clicksebuf : kag.numSEBuffers - 1;

        var select = new MapSelectButtonLayer(window, this, info);

        select.pos     = elm.pos !== void ? elm.pos : "default";
        select.onenter = MessageLayer.createSoundExpression(elm.onenter, enterse, entersebuf);
        select.onleave = MessageLayer.createSoundExpression(elm.onleave, leavese, leavesebuf);
        select.onclick = MessageLayer.createSoundExpression(elm.exp,     clickse, clicksebuf),
        select.storage = elm.storage;
        select.target  = elm.target;
        
        selects.add(select);
    }

    /**
     * xkI理_始
     */
    function start(parent, absolute) {

        this.parent = parent;
        this.absolute = absolute;
        
        var posall   = %[];
        
        // xk肢を鏊ごとに分
        for (var i=0; i<selects.count;i++) {
            var select = selects[i];
            if (posall[select.pos] === void) {
                posall[select.pos] = [];
            }
            posall[select.pos].add(select);
        }

        // 鏊ごとにボタンを再配置する
        foreach(posall, function(posname,selects,posall,self) {
            
            var pos = self.positions[posname];
            if (pos === void) {
                pos = self.positions["default"];
            }
            if (pos !== void) {

                // 鏊の{整I理
                var left = pos.left - (selects.count * self.buttonWidth) / 2;
                for (var i=0; i<selects.count;i++) {
                    var select = selects[i];
                    select.setPos(left + i * self.buttonWidth + ((self.buttonWidth - select.width) / 2),
                                  pos.top - select.height / 2);
                    // Ｙ座摔大きいほうが上、左にいるほうが上
                    select.absolute = pos.top * 1000 + select.left;
                    select.visible = true;
                    //dm("表示:" + select.left  +"," + select.top);
                }
            }
        },this);

        visible = true;
        timer.interval = baseInterval / 2;
        timer.enabled  = true;
    }

    /**
     * 完了
     */
    function done() {
        visible = false;
        timer.enabled  = false;
        clear();
    }
    
    /**
     * I理K了
     */
    function onButtonClick(select) {
        if (select !== void) {
            Scripts.eval(select.onclick) if select.onclick != '';
            if (select isvalid) {
                if (select.storage != '' || select.target != '')	{
                    window.lockMessageLayerSelProcess(); // xkをロック
                    if (window.getKeyState(VK_RETURN) || window.getKeyState(VK_SPACE) || window.getKeyState(VK_CONTROL))
                        window.hideMouseCursor();
                    // キ`ボ`ドによる操作の龊悉膝蕙Ε攻`ソルをLす
                    window.process(select.storage, select.target);
                } else {
                    window.processGo();
                }
                window.doneMapSelect();
            }
        }
    }

    var invisibleByUser = false; // ユ`ザにより一r的に不可
	var visibleBeforeUserInvisible  = false;
	function setHiddenStateByUser(b)
	{
		// ユ`ザが右クリックなどでメッセ`ジレイヤを一r的にLすときに
		// 呼ばれる
		if(b)
		{
			visibleBeforeUserInvisible = visible;
			invisibleByUser = true; // ユ`ザにより一r的に不可
			visible = false;
		}
		else
		{
			invisibleByUser = false; // 可
			visible = visibleBeforeUserInvisible;
		}
	}

    function lockFocus() {
        for (var i=0; i<selects.count;i++) {
            var select = selects[i];
            select.focusable = false;
        }
    }
    
    function unlockFocus() {
        for (var i=0; i<selects.count;i++) {
            var select = selects[i];
            select.focusable = true;
        }
    }

}


