// SE.tjs - 抗音管理
// Copyright (C)2001-2006, W.Dee and contributors  改?配布は自由です

class SESoundBuffer extends WaveSoundBuffer
{
	// 抗音クラス ( WaveSoundBuffer を@承 )

	var owner; // オ`ナ`
	var id; // 抗音バッファID
	var inFading; // フェ`ド中かどうか
	var prevstatus = "unload"; // 直前のステ`タス
	var currentStorage = ""; // かけの演奏中のストレ`ジ
	var currentVolume = 100; // かけのボリュ`ム
	var inFadeAndStop = false; // フェ`ドK了rに停止するか
    
	function SESoundBuffer(owner, id)
	{
		// コンストラクタ
		super.WaveSoundBuffer(owner);

		this.owner = owner;
		this.id = id;
	}

	function finalize()
	{
		// finalize()
		super.finalize(...);
	}

	function play(elm, resetvolume = true)
	{
		// play オ`バ`ライド
		super.stop();
		stopFade();
		var storage = elm.storage;
        var start = elm.start;
        var found = true;
		if(!Storages.isExistentStorage(storage))
		{
			var test;
			if(test = storage + ".wav", Storages.isExistentStorage(test))
				storage = test;
			else if(test = storage + ".ogg", Storages.isExistentStorage(test))
				storage = test;
			else if(test = storage + ".tcw", Storages.isExistentStorage(test))
				storage = test;
			else if(test = storage + ".mp3", Storages.isExistentStorage(test))
				storage = test;
			else
				found = false;
		}
		if(!found)
			throw new Exception("音效 " + storage + " 无法找到");
		var loop = elm.loop === void ? false : +elm.loop;
		looping = loop;
		if(loop)
			currentStorage = storage;
		else
			currentStorage = "";
		try
		{
            super.open(storage);
            if(resetvolume) super.volume = currentVolume * 1000;
            // 再生位置指定
            if (start !== void &&
                super.labels !== void &&
                (start = super.labels[start]) !== void &&
                (start = start.samplePosition) !== void) {
                super.samplePosition = start;
            }
            super.play();
		}
		catch(e)
		{
			dm("无法播放音效 : " + e.message);
		}
	}

	function stop()
	{
		// stop オ`バ`ライド
		currentStorage = "";
		try
		{
			super.stop(...);
		}
		catch(e)
		{
			dm("无法停止音效 : " + e.message);
		}
		inFadeAndStop = false;
	}

	function stopFade()
	{
		// stopFade オ`バ`ライド
		try
		{
			super.stopFade();
		}
		catch(e)
		{
			dm("无法淡入淡出音效 : " + e.message);
		}
		inFadeAndStop = false;
	}

	function fade(elm)
	{
		// fade オ`バ`ライド
		inFading = true;
		var time = elm.time === void ? 5000 : +elm.time;
		var vol = +elm.volume * 1000;
		currentVolume = +elm.volume;
		try
		{
			super.fade(vol, time);
		}
		catch(e)
		{
			dm("无法淡入淡出音效 : " + e.message);
		}
	}

	function fadeIn(elm)
	{
		// フェ`ドインしながらの再生
		super.volume = 0;
		play(elm, false);
		inFading = true;
		var time = elm.time === void ? 5000 : +elm.time;
		var vol = currentVolume * 1000;
		try
		{
			super.fade(vol, time);
		}
		catch(e)
		{
			dm("无法淡入淡出音效 : " + e.message);
		}
	}

	function fadeOut(elm)
	{
		// フェ`ドアウト後停止
		inFading = true;
		inFadeAndStop = true;
		var time = elm.time === void ? 5000 : +elm.time;
		try
		{
			super.fade(0, time);
		}
		catch(e)
		{
			dm("无法淡入淡出音效 : " + e.message);
		}
	}

	function onFadeCompleted()
	{
		// onFadeCompleted オ`バ`ライド
		inFading = false;
		if(inFadeAndStop)
		{
			try
			{
				super.stop(); // 再生停止
			}
			catch(e)
			{
				dm("无法停止音效 : " + e.message);
			}
			inFadeAndStop = false;
		}
        super.onFadeCompleted(...);
		owner.onSESoundBufferFadeCompleted(id); // フェ`ドK了
	}

	function onStatusChanged()
	{
		// onStatusChanged オ`バ`ライド
		// ステ`タスが涓された
		super.onStatusChanged(...);
		var ps = prevstatus;
		var cs = status;
		prevstatus = cs;
		if(ps == "play" && cs == "stop")
		{
			currentStorage = "";
			owner.onSESoundBufferStop(id); // play => stop : 演奏が停止した
		}
	}

	function canWaitStop()
	{
		// K了を待てるか
		return status == "play" && !looping;
	}

	property volume
	{
		setter(x)
		{
			currentVolume = x;
			super.volume = x * 1000;
		}
		getter
		{
			return super.volume \ 1000;
		}
	}

	property pan
	{
		setter(x)
		{
			super.pan = x * 1000;
		}
		getter
		{
			return super.pan \ 1000;
		}
	}

	function setOptions(elm)
	{
		if(elm.volume !== void) volume = +elm.volume;
		if(elm.gvolume !== void)
		{
			// 大域ボリュ`ム
			var sysvar = owner.scflags; // システム(コア)涫
			if(sysvar.se === void) sysvar.se = [];
			sysvar = sysvar.se;
			if(sysvar[id] === void) sysvar[id] = %[];
			sysvar = sysvar[id];
			var v2 = +elm.gvolume;
			v2 = int(v2 * 1000);
			sysvar.globalVolume = v2;
            if (sysvar.enable === void || sysvar.enable) {
                volume2 = v2;
            } else {
                volume2 = 0;
            }
		}
		if(elm.pan !== void)
		{
			pan = +elm.pan;
		}
	}

	function store()
	{
		// 辞配列にF在の状Bをhする
		var dic = %[];
		dic.currentStorage = currentStorage;
		dic.volume = currentVolume;
		dic.pan = pan;
		return dic;
	}

	function restore(dic)
	{
		// 辞配列から状Bをiみ出し、する
		currentVolume = dic.volume;
		pan = dic.pan;
		if(dic.currentStorage != "")
			play(%[storage : dic.currentStorage, loop : true]);
		else
			stop();
	}

	function restoreSystemState(dic)
	{
		// システム涫 dic から情螭蛟O定する
		// 大域ボリュ`ムの情螭虻盲
		var sysvar = dic.se;
		if(sysvar !== void)
		{
			sysvar = sysvar[id];
			if(sysvar !== void)
			{
                if (sysvar.enable === void || sysvar.enable) {
                    var v2 = sysvar.globalVolume;
                    if(v2 !== void) {
                        volume2 = +v2;
                    }
                } else {
                    volume2 = 0;
                }
            }
		}
	}
}

